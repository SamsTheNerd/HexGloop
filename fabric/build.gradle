plugins {
	// id 'fabric-loom' version '1.1-SNAPSHOT'
	// id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.1.2" 
    id "me.modmuss50.mod-publish-plugin" version "0.3.0"
}

apply from: '../common/build.gradle'

// sourceCompatibility = JavaVersion.VERSION_17
// targetCompatibility = JavaVersion.VERSION_17

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    // mixin.defaultRefmapName = "hexcasting.mixins.refmap.json"
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations.all {
   resolutionStrategy {
     force("net.fabricmc:fabric-loader:${project.fabric_loader_version}")
     force("dev.architectury:architectury-fabric:${rootProject.architectury_version}")
   }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    // To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    runtimeOnly(include("com.github.llamalad7.mixinextras:mixinextras-fabric:${mixinExtrasVersion}"))

	// for REI, making sure it doesn't pop up with the keyboard
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
    modRuntimeOnly("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}")
	// modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${project.rei_version}"

	// for HexCasting
	modImplementation "at.petra-k.hexcasting:hexcasting-fabric-${project.minecraft_version}:${project.hexcastingVersion}"

    modImplementation "maven.modrinth:wnboi:${wnboiVersion}-fabric"
    // modRuntimeOnly "maven.modrinth:wnboi:${wnboiVersion}-fabric"

    modRuntimeOnly "maven.modrinth:hexal:${hexalVersion}"
    modRuntimeOnly "maven.modrinth:moreiotas:${moreIotasVersion}"

    modImplementation "software.bernie.geckolib:geckolib-fabric-1.19:${geckolibVersion}"
    modRuntimeOnly "software.bernie.geckolib:geckolib-fabric-1.19:${geckolibVersion}"

	// Hex Casting Dependencies
	modImplementation "at.petra-k.paucal:paucal-fabric-${minecraft_version}:${paucalVersion}"
	modImplementation "vazkii.patchouli:Patchouli:${minecraft_version}-${patchouliVersion}-FABRIC"

    modImplementation "dev.emi:trinkets:${trinkets_version}"

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-api:${cardinalComponentsVersion}"

    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:${entityReachVersion}"
    include "com.jamieswhiteshirt:reach-entity-attributes:${entityReachVersion}"

    modImplementation "io.github.tropheusj:serialization-hooks:${serializationHooksVersion}"
    include "io.github.tropheusj:serialization-hooks:${serializationHooksVersion}"

	modImplementation "maven.modrinth:lithium:${lithiumVersion}"
    modRuntimeOnly "maven.modrinth:phosphor:${phosphorVersion}"
	modImplementation "maven.modrinth:sodium:${sodiumVersion}"

    modRuntimeOnly "maven.modrinth:tooltipfix:${tooltipfixVersion}"

     // for sodium matrices?
	implementation group: "org.joml", name: "joml", version: "1.10.5"
	include group: "org.joml", name: "joml", version: "1.10.5"


    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    // injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set "fabric"
}

jar {
    archiveClassifier.set "fabric-dev"
}

sourcesJar {
    archiveClassifier.set "fabric-sources"
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

publishMods {
    file = remapJar.archiveFile
    additionalFiles.from(sourcesJar.archiveFile)
    changelog = trimChangelog(file("$rootDir/changelog.md").text)
    type = STABLE
    modLoaders.add("fabric")
    modLoaders.add("quilt")

    version = project.version + "-fabric"
    displayName = "Hex Gloop" + " " + project.version + " [Fabric]"
 
    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
        projectId = "897558"
        minecraftVersions.add("1.19.2")
        requires {
            slug = "architectury-api"
        }
        requires {
            slug = "hexcasting"
        }
        requires {
            slug = "wnboi"
        }
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_API_KEY")
        projectId = "ryfyOhoP"
        minecraftVersions.add("1.19.2")

        requires {
            // architectury api
            // id = "XV72M5Gg"
            slug = "architectury-api"
        }
        requires {
            slug = "hex-casting"
        }
        requires{
            // fabric api
            // id = "hfsU4hXq"
            slug = "fabric-api"
        }
        requires {
            slug = "wnboi"
        }
        optional {
            slug="trinkets"
        }
    }
}